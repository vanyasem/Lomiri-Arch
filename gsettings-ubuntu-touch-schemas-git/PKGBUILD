# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=gsettings-ubuntu-touch-schemas-git
_pkgname=gsettings-ubuntu-touch-schemas
pkgver=r199.0d029fa
pkgrel=2
pkgdesc='GSettings desktop-wide schemas for Ubuntu Touch'
url='https://github.com/ubports/gsettings-ubuntu-touch-schemas'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL2.1)
conflicts=(gsettings-ubuntu-touch-schemas)
provides=(gsettings-ubuntu-touch-schemas)
depends=(dconf)
makedepends=(git gnome-common intltool)
source=('git+https://github.com/ubports/gsettings-ubuntu-touch-schemas.git')
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  ./autogen.sh \
      --prefix=/usr \
      --localstatedir=/var
  make
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
