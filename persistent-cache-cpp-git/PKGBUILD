# Maintainer: Marius Gripsgard <marius@ubports.com>

pkgname=persistent-cache-cpp-git
_pkgname=persistent-cache-cpp
pkgver=r108.57ccf1a
pkgrel=1
pkgdesc='A cache of key-value pairs with persistent storage for C++'
url='https://gitlab.com/ubports/core/lib-cpp/persistent-cache-cpp'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL)
conflicts=(persistent-cache-cpp)
provides=(persistent-cache-cpp)
depends=(boost leveldb valgrind)
makedepends=(bzr cmake cmake-extras-git doxygen graphviz lcov gcovr gtest)
source=("git+https://gitlab.com/ubports/core/lib-cpp/persistent-cache-cpp.git")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr .
  make
}

check() {
  cd ${_pkgname}
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
