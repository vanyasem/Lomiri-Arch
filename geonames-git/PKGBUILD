# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=geonames-git
_pkgname=geonames
pkgver=r5.ce158ce
pkgrel=1
pkgdesc='Parse and query the geonames database dump'
url='https://github.com/ubports/geonames'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(geonames)
provides=(geonames)
makedepends=(git gtk-doc)
source=('git+https://bitbucket.org/vanyasem/geonames')
sha256sums=('SKIP')

BUILDENV+=('!check') # Weird locale issues

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

build() {
  cd ${_pkgname}
  ./autogen.sh --prefix=/usr
  make
}

check() {
  cd ${_pkgname}
  make check
}

package() {
  cd ${_pkgname}
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
