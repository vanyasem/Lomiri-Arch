# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor Spike29 <leguen.yannick@gmail.com>

pkgname=qdjango-git
_pkgname=qdjango
pkgver=v0.6.2+16+gbda4755
pkgrel=1
pkgdesc='A Qt-based C++ web framework'
url='https://github.com/jlaine/qdjango/'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL2.1)
depends=(qt5-base)
provides=(qdjango)
conflicts=(qdjango)
source=('git+https://github.com/jlaine/qdjango.git')
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/-/+/g'
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  qmake PREFIX=/usr ..
  make
}

package() {
  cd ${_pkgname}/build
  make INSTALL_ROOT="${pkgdir}/" install
  rm -r ${pkgdir}/usr/tests/
}
# vim:set ts=2 sw=2 et:
