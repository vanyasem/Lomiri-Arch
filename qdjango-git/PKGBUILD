# Maintainer: Ivan Semkin (ivan at semkin dot ru)
# Contributor Spike29 <leguen.yannick@gmail.com>

pkgname=qdjango-git
_pkgname=qdjango
pkgver=v0.6.2+16+gbda4755
pkgrel=2
pkgdesc='A Qt-based C++ web framework'
url='https://github.com/jlaine/qdjango/'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL2.1)
conflicts=(qdjango)
provides=(qdjango)
depends=(qt5-base)
makedepends=(git)
provides=(qdjango)
conflicts=(qdjango)
source=('git+https://github.com/jlaine/qdjango.git' qdjango.patch)
sha256sums=('SKIP'
            '74e802a40c57b0685a0bd951884d476b273e775551338b5e7e0212fabd53e974')

pkgver() {
  cd ${_pkgname}
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd ${_pkgname}
  patch -Np1 -i ../qdjango.patch
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  qmake PREFIX=/usr ..
  make
}

package() {
  cd ${_pkgname}/build
  make INSTALL_ROOT="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
