# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=system-settings-git
_pkgname=system-settings
pkgver=r7731.eab0d2ec
pkgrel=1
pkgdesc='System Settings application for Unity8'
url='https://github.com/ubports/system-settings'
arch=(x86_64 i686 armv7h aarch64)
license=(GPL3)
conflicts=(system-settings)
provides=(system-settings)
depends=(trust-store-git click-git gnome-desktop ubuntu-app-launch-git geonames-git libqtdbusmock-git ubuntu-ui-toolkit-git gsettings-qt-git accountsservice-ubuntu upower ubuntu-themes qmenumodel-git libqofono content-hub-git)
makedepends=(git cmake intltool)
checkdepends=(xorg-server-xvfb)
source=('git+https://github.com/ubports/system-settings.git')
sha256sums=('SKIP')

BUILDENV+=('!check') # Some tests fail

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd ${_pkgname}
  git checkout xenial

  sed -i 's/add_subdirectory(system-update)//g' plugins/CMakeLists.txt
  sed -i 's/add_subdirectory(system-update)//g' tests/plugins/CMakeLists.txt

  sed -i 's/add_subdirectory(Update)//g' tests/mocks/Ubuntu/SystemSettings/CMakeLists.txt
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DENABLE_TESTS=off ..
  make
}

check() {
  cd ${_pkgname}/build
  make ARGS+="--output-on-failure" test
}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
