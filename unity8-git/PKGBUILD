# Maintainer: Ivan Semkin (ivan at semkin dot ru)

pkgname=unity8-git
_pkgname=unity8
pkgver=r18213.d07cfbed2
pkgrel=1
pkgdesc='Unity 8 shell'
url='https://github.com/ubports/unity8'
arch=(x86_64 i686 armv7h aarch64)
license=(LGPL3)
conflicts=(unity8)
provides=(unity8)
depends=(lcov gcovr geonames-git unity-api-git qmenumodel-git gnome-desktop ubuntu-app-launch-git ubuntu-ui-toolkit-git libqtdbustest-git libqtdbusmock-git system-settings-git indicator-network-git libusermetrics-git liblightdm-qt5 settings-components-git gsettings-ubuntu-touch-schemas-git)
makedepends=(git cmake cmake-extras-git doxygen)
checkdepends=(xorg-server-xvfb)
source=('git+https://github.com/ubports/unity8.git'
        'AndroidAway.patch'
        'NoDpkgParse.patch'
        'no-lights.patch')
sha256sums=('SKIP'
            'ba26a1235461f531f9961a306da22593db92d3bd08a84139ef4ae2d042a2b1cc'
            'SKIP'
            'SKIP')

BUILDENV+=('!check') # https://paste.ubuntu.com/p/2d33RJX4W8/

pkgver() {
  cd ${_pkgname}
  echo "r$(git rev-list --count HEAD).$(git describe --always)"
}

prepare() {
  cd ${_pkgname}
  git checkout xenial

  sed -i 's/ubuntu-app-launch-2/ubuntu-app-launch-3/g' CMakeLists.txt
  
  patch -Np1 -i "${srcdir}/AndroidAway.patch"
  patch -Np1 -i "${srcdir}/NoDpkgParse.patch"
  patch -Np1 -i "${srcdir}/no-lights.patch"
}

build() {
  cd ${_pkgname}
  mkdir -p build
  cd build
  cmake -DCMAKE_INSTALL_PREFIX:PATH=/usr -DCMAKE_INSTALL_LIBDIR="lib/" -DNO_TESTS=true ..
  make
}

#check() {
#  cd ${_pkgname}/build
#  make ARGS+="--output-on-failure" test
#}

package() {
  cd ${_pkgname}/build
  make DESTDIR="${pkgdir}/" install
}
# vim:set ts=2 sw=2 et:
